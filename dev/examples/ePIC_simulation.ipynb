{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Working with ePIC Simulation Output\n",
    "This is an example using ePIC simulated data inspired by the tutorial prepared for the Hyderabad Hackathon."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Installation and configuration\n",
    "You need the following packages installed: EDM4hep, Plots, LinearAlgebra, Combinatorics.\n",
    "\n",
    "This is done simply by activating and instantiating the environment where this notebook is located.\n",
    "Please note that the package `EDM4hep` needs to be configured to use the `eic` data model.\n",
    "Setting the event model is needed to be done only the first time\n",
    "\n",
    "```\n",
    "   import Pkg; Pkg.activate(@__DIR__); Pkg.instantiate()\n",
    "   using EDM4hep\n",
    "   EDM4hep.set_edmodel(\"eic\")  # set the EDM model to use\n",
    "```"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Loading the necessary packages"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using EDM4hep\n",
    "using EDM4hep.RootIO\n",
    "using EDM4hep.Histograms\n",
    "using EDM4hep.Analysis\n",
    "using Plots\n",
    "using LinearAlgebra"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Open the data file"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "fname = \"pythia8NCDIS_18x275_minQ2=10_beamEffects_xAngle=-0.025_hiDiv_1.0000.eicrecon.edm4eic.root\"\n",
    "\n",
    "# eic_server = \"root://dtn-eic.jlab.org/\"\n",
    "# fpath = \"/volatile/eic/EPIC/RECO/25.08.0/epic_craterlake/DIS/NC/18x275/minQ2=10/\"\n",
    "# file = eic_server * fpath * fname\n",
    "\n",
    "file  = joinpath(@__DIR__, \"../../../examples/ePIC\", fname)\n",
    "reader = RootIO.Reader(file)\n",
    "events = RootIO.get(reader, \"events\");"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Basic Plotting Introduction\n",
    "We will begin by taking a look at some of our reconstructed charged particles.\n",
    "The collection is called `ReconstructedChargedParticles` and the sub-branch `energy`.\n",
    "Note that in ROOT (and Python) you would access it with `ReconstructedChargedParticles.energy`,\n",
    "with UnROOT the separator is with `_`.\n",
    "\n",
    "Note also that the returned type for `events.ReconstructedChargedParticles_energy` is a `Vector` of `Vector`s.\n",
    "The first vector is for the events and the second is for the particles in each event.\n",
    "Therefore, we need to flatten them with the function `vcat`"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "histogram(vcat(events.ReconstructedChargedParticles_energy...), bins=range(1,50,100))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can separate the positively and negatively charged particles"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "positive = vcat(events.ReconstructedChargedParticles_charge...) .> 0\n",
    "negative = vcat(events.ReconstructedChargedParticles_charge...) .< 0\n",
    "histogram(vcat(events.ReconstructedChargedParticles_energy...)[positive], bins=range(0,50,100))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can plot both at the same time"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "histogram(vcat(events.ReconstructedChargedParticles_energy...)[negative], label=\"negative\", bins=range(0,50,100), xlabel=\"GeV\")\n",
    "histogram!(vcat(events.ReconstructedChargedParticles_energy...)[positive], label=\"positive\", bins=range(0,50,100), xlabel=\"GeV\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Basic Analysis\n",
    "In this section, we will make some basic analysis plots checking the detection efficiency and resolution for some of our particles."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Please note that we define getter functions only reading the sub-branches that we are going to use\n",
    "in the analysis."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "get_charged = RootIO.create_getter(reader, \"ReconstructedChargedParticles\"; selection=[:energy, :momentum, :charge])\n",
    "get_asso    = RootIO.create_getter(reader, \"ReconstructedChargedParticleAssociations\")\n",
    "get_mcps    = RootIO.create_getter(reader, \"MCParticles\"; selection=[:PDG, :momentum, :charge, :mass, :parents, :daughters])\n",
    "\n",
    "# Basic resolution histogram\n",
    "hresolu = H1D(\"Energy resolution [%]\", 100, -10., 10.)\n",
    "\n",
    "# Loop over events and fill histogram\n",
    "for evt in events\n",
    "    recps  = get_charged(evt)     # Get the coll. of reconstructed charged particles\n",
    "    assocs = get_asso(evt)        # Get the coll. of associations `rec <-> mcp`\n",
    "    mcps   = get_mcps(evt)        # Get the coll. of MC particles. Used to get .energy\n",
    "\n",
    "    for recp in recps             # Loop over reconstructed particles\n",
    "        ind = findfirst(x -> x.rec == recp, assocs)  # Find the association to ReconstructedParticle\n",
    "        isnothing(ind) && continue                   # If no association found, skip to next recp\n",
    "        ΔE = recp.energy - assocs[ind].sim.energy    # Calculate energy resolution\n",
    "        push!(hresolu, 100*ΔE/recp.energy)           # Fill histogram\n",
    "    end\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Plot the result"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "plot(hresolu)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Event model access and navigation\n",
    "We have seen the way to obtain the collections of objects for each event in the previous simple analysis.\n",
    "Once we have a collection, the `object` attributes can be accessed naturally with the (.) notation including relationships (1-to-1 or 1-N)."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Following relationships\n",
    "See the following example with the decay products of a `MCParticle`."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "mcps = get_mcps(events[1])                   # Get the collection of MC particles in the first event\n",
    "for mcp in mcps[1:10]                        # Loop over the first N MC particles\n",
    "    length(mcp.daughters) == 2 || continue   # only consider particles with 2 daughters\n",
    "    println(mcp.name, \"  Energy: \", mcp.energy, \" Status: \", mcp.generatorStatus)\n",
    "    for d in mcp.daughters\n",
    "        println(\"   --->: \", d.name, \"  Energy: \", d.energy)\n",
    "    end\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Getting the documentation of EDM4hep types\n",
    "For example we can get the attributes available in a `ReconstructedParticle`"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "display(\"text/markdown\", Base.Docs.doc(ReconstructedParticle))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "### Column access and operations\n",
    "Lets sum the energy of all MCParticles that have no daughters (i.e. final state particles)\n",
    "to verify energy conservation in the event simulation."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "mcps[length.(mcps.daughters) .== 0].energy |> sum"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Should be equal or close to the sum of the energy of the incident electron and proton (18+275)\n",
    "Let's see what is the distribution for all events"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "hsumenergy = H1D(\"Summed energy final particles\", 100, 200., 300.; unit=:GeV)\n",
    "for evt in events\n",
    "    mcps   = get_mcps(evt)        # Get the coll. of MC particles. Used to get .energy\n",
    "    sumenergy = mcps[length.(mcps.daughters) .== 0].energy |> sum\n",
    "    push!(hsumenergy, sumenergy)\n",
    "end\n",
    "plot(hsumenergy)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.1",
   "language": "julia"
  }
 },
 "nbformat": 4
}