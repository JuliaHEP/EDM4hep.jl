{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# FCCee Analysis Examples (MT)\n",
    "\n",
    "Using the example `higgs/mH-recoil/mumu` from [FCCAnalyses](https://github.com/HEP-FCC/FCCAnalyses)"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Load the necessary modules"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using EDM4hep\n",
    "using EDM4hep.RootIO\n",
    "using EDM4hep.Analysis\n",
    "using EDM4hep.SystemOfUnits\n",
    "using Plots\n",
    "theme(:boxed)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Definition of some analysis functions\n",
    "These are couple of examples of high-level functions that makes use of `ReconstructedParticle`\n",
    "objects to build resonances and recoils.\n",
    "They make use of standard Julia functions to generate combinations, to sort a vector,\n",
    "and to work with LorentzVectors."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "re-using convenient existing packages"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using LorentzVectorHEP\n",
    "using Combinatorics\n",
    "\n",
    "\"\"\"\n",
    "    resonanceBuilder(rmass::AbstractFloat, legs::AbstractVector{ReconstructedParticle})\n",
    "\n",
    "Returns a container with the best resonance of 2 by 2 combinatorics of the `legs` container\n",
    "sorted by closest to the input `rmass` in absolute value.\n",
    "\"\"\"\n",
    "function resonanceBuilder(rmass::AbstractFloat, legs::AbstractVector{ReconstructedParticle})\n",
    "    result = ReconstructedParticle[]\n",
    "    length(legs) < 2 && return result\n",
    "    for (a,b) in combinations(legs, 2)\n",
    "        lv = LorentzVector(a.energy, a.momentum...) + LorentzVector(b.energy, b.momentum...)\n",
    "        rcharge = a.charge + b.charge\n",
    "        push!(result, ReconstructedParticle(mass=mass(lv), momentum=(lv.x, lv.y, lv.z), charge=rcharge))\n",
    "    end\n",
    "    sort!(result, lt =  (a,b) -> abs(rmass-a.mass) < abs(rmass-b.mass))\n",
    "    return result[1:1]  # take the best one\n",
    "end;\n",
    "\n",
    "\"\"\"\n",
    "    recoilBuilder(comenergy::AbstractFloat, legs::AbstractVector{ReconstructedParticle})\n",
    "\n",
    "    build the recoil from an arbitrary list of input `ReconstructedParticle`s and the center of mass energy.\n",
    "\"\"\"\n",
    "function recoilBuilder(comenergy::AbstractFloat, in::AbstractVector{ReconstructedParticle})\n",
    "    result = ReconstructedParticle[]\n",
    "    isempty(in) && return result\n",
    "    recoil_lv = LorentzVector(comenergy, 0, 0, 0)\n",
    "    for p in in\n",
    "        recoil_lv -= LorentzVector(p.mass, p.momentum...)\n",
    "    end\n",
    "    push!(result, ReconstructedParticle(mass=mass(recoil_lv), momentum=(recoil_lv.x, recoil_lv.y, recoil_lv.z)))\n",
    "    return result\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Defining the resulting analysis data\n",
    "We create a custom structure with all summary information of each event.\n",
    "It includes a `DataFrame` in  which we will collect the information."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using DataFrames\n",
    "\n",
    "mutable struct AnalysisData <: AbstractAnalysisData\n",
    "    df::DataFrame\n",
    "    pevts::Int64\n",
    "    sevts::Int64\n",
    "    AnalysisData() = new(DataFrame(Zcand_m = Float32[], Zcand_recoil_m = Float32[], Zcand_q = Int32[], Zcand_recoil_θ = Float32[]), 0, 0)\n",
    "end\n",
    "# Need to tell how to merge two DataFramnes\n",
    "Base.merge!(df1::DataFrame, df2::DataFrame) = append!(df1, df2)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Open the data file to get the events\n",
    "- It is using a file in EOS with the `root:` protocol\n",
    "- The obtained `events` is a `LazyTree` created by the [UnROOT.jl](https://github.com/JuliaHEP/UnROOT.jl) package.\n",
    "  As the name indicates, the event is actually yet read."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# f = \"root://eospublic.cern.ch//eos/experiment/fcc/ee/generation/DelphesEvents/winter2023/IDEA/p8_ee_ZZ_ecm240/events_000189367.root\"\n",
    "f = joinpath(@__DIR__, \"../../../examples/FCC/events_000189367.root\")\n",
    "\n",
    "reader = RootIO.Reader(f);\n",
    "events = RootIO.get(reader, \"events\");"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The `reader` captures useful information"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "reader"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Define an analysis function\n",
    "This function will loop over events (of a chunk of the full set of events) and fill the user defined data structure"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "function myanalysis!(data::AnalysisData, reader, events)\n",
    "    for evt in events\n",
    "        data.pevts += 1\n",
    "        muids = RootIO.get(reader, evt, \"Muon#0\")     # get the ObjectIDs of Muons\n",
    "        length(muids) < 2 && continue                 # need at least 2 muons to build a Z\n",
    "        recps = RootIO.get(reader, evt, \"ReconstructedParticles\")\n",
    "        muons = recps[muids]                          # use the objectIDs to collect the referenced ReconstructedParticles\n",
    "        sel_muons = filter(x -> pₜ(x) > 10GeV, muons)  # select muons with pT > 10 GeV\n",
    "        zed_leptonic = resonanceBuilder(91GeV, sel_muons)\n",
    "        zed_leptonic_recoil = recoilBuilder(240GeV, zed_leptonic)\n",
    "        if length(zed_leptonic) == 1                  #  Filter to have exactly one Z candidate\n",
    "            Zcand_m        = zed_leptonic[1].mass\n",
    "            Zcand_recoil_m = zed_leptonic_recoil[1].mass\n",
    "            Zcand_recoil_θ = zed_leptonic_recoil[1].momentum |> EDM4hep.θ\n",
    "            Zcand_q        = zed_leptonic[1].charge\n",
    "            if 80GeV <= Zcand_m <= 100GeV\n",
    "                push!(data.df, (Zcand_m, Zcand_recoil_m, Zcand_q, Zcand_recoil_θ))\n",
    "                data.sevts += 1\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return data\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Get the number of cores available and instantiate the user analysis data"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "N = Threads.nthreads()\n",
    "data = AnalysisData();"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Execute the analysis\n",
    "Perform the analysis with and without multi-threading"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "elapsed1 = @elapsed do_analysis!(data, myanalysis!, reader, events; mt=false)\n",
    "println(\"Serial: total time: $elapsed1, $(data.pevts/elapsed1) events/s. Selected events: $(data.sevts)\")\n",
    "\n",
    "elapsed2 = @elapsed do_analysis!(data, myanalysis!, reader, events; mt=true)\n",
    "println(\"MT[$N]: total time: $elapsed2, $(data.pevts/elapsed2) events/s. Selected events: $(data.sevts)\")\n",
    "println(\"Speeedup: $(elapsed1/elapsed2)\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Let's repeat the analysis with and without multi-threading"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "data = AnalysisData()\n",
    "elapsed1 = @elapsed do_analysis!(data, myanalysis!, reader, events; mt=false)\n",
    "println(\"Serial: total time: $elapsed1, $(data.pevts/elapsed1) events/s. Selected events: $(data.sevts)\")\n",
    "\n",
    "elapsed2 = @elapsed do_analysis!(data, myanalysis!, reader, events; mt=true)\n",
    "println(\"MT[$N]: total time: $elapsed2, $(data.pevts/elapsed2) events/s. Selected events: $(data.sevts)\")\n",
    "println(\"Speeedup: $(elapsed1/elapsed2)\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Plot the results\n",
    "Z candidates"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "histogram(data.df.Zcand_m, title=\"Resonance mass plot\",xlabel=\"GeV\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Z candidates recoils"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "histogram(data.df.Zcand_recoil_m, title=\"Z leptonic recoil\",xlabel=\"GeV\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.1",
   "language": "julia"
  }
 },
 "nbformat": 4
}