{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# FCCee Analysis Examples\n",
    "\n",
    "Using the example `higgs/mH-recoil/mumu` from [FCCAnalyses](https://github.com/HEP-FCC/FCCAnalyses)"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Load the necessary modules"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using EDM4hep\n",
    "using EDM4hep.RootIO\n",
    "using EDM4hep.SystemOfUnits\n",
    "using EDM4hep.Histograms\n",
    "using Plots; gr()\n",
    "theme(:boxed)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Definition of some analysis functions\n",
    "These are couple of examples of high-level functions that makes use of `ReconstructedParticle`\n",
    "objects to build resonances and recoils.\n",
    "They make use of standard Julia functions to generate combinations, to sort a vector,\n",
    "and to work with LorentzVectors."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "re-using convenient existing packages"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using LorentzVectorHEP\n",
    "using Combinatorics\n",
    "\n",
    "\"\"\"\n",
    "    resonanceBuilder(rmass::AbstractFloat, legs::AbstractVector{ReconstructedParticle})\n",
    "\n",
    "Returns a container with the best resonance of 2 by 2 combinatorics of the `legs` container\n",
    "sorted by closest to the input `rmass` in absolute value.\n",
    "\"\"\"\n",
    "function resonanceBuilder(rmass::AbstractFloat, legs::AbstractVector{ReconstructedParticle})\n",
    "    result = ReconstructedParticle[]\n",
    "    length(legs) < 2 && return result\n",
    "    for (a,b) in combinations(legs, 2)\n",
    "        lv = LorentzVector(a.energy, a.momentum...) + LorentzVector(b.energy, b.momentum...)\n",
    "        rcharge = a.charge + b.charge\n",
    "        push!(result, ReconstructedParticle(mass=mass(lv), momentum=(lv.x, lv.y, lv.z), charge=rcharge))\n",
    "    end\n",
    "    sort!(result, lt =  (a,b) -> abs(rmass-a.mass) < abs(rmass-b.mass))\n",
    "    return result[1:1]  # take the best one\n",
    "end;\n",
    "\n",
    "\"\"\"\n",
    "    recoilBuilder(comenergy::AbstractFloat, legs::AbstractVector{ReconstructedParticle})\n",
    "\n",
    "    build the recoil from an arbitrary list of input `ReconstructedParticle`s and the center of mass energy.\n",
    "\"\"\"\n",
    "function recoilBuilder(comenergy::AbstractFloat, in::AbstractVector{ReconstructedParticle})\n",
    "    result = ReconstructedParticle[]\n",
    "    isempty(in) && return result\n",
    "    recoil_lv = LorentzVector(comenergy, 0, 0, 0)\n",
    "    for p in in\n",
    "        recoil_lv -= LorentzVector(p.mass, p.momentum...)\n",
    "    end\n",
    "    push!(result, ReconstructedParticle(mass=mass(recoil_lv), momentum=(recoil_lv.x, recoil_lv.y, recoil_lv.z)))\n",
    "    return result\n",
    "end;"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Defining the histograms\n",
    "We create a custom structure with all the histograms intialized with their binning,\n",
    "units and titles. We use and the way of plotting them.\n",
    "We use the module `Parameters` that allows to create user structures with defaults."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "myhists = (\n",
    "    mz          = H1D(\"m_{Z}\",125,0,250, unit=:GeV),\n",
    "    mz_zoom     = H1D(\"m_{Z} (zoom)\",40,80,100, unit=:GeV),\n",
    "    lr_m        = H1D(\"Z leptonic recoil\", 100, 0, 200, unit=:GeV),\n",
    "    lr_m_zoom   = H1D(\"Z leptonic recoil (zoom)\", 200, 80, 160, unit=:GeV),\n",
    "    lr_m_zoom1  = H1D(\"Z leptonic recoil (zoom)\", 100, 120, 140, unit=:GeV),\n",
    "    lr_m_zoom2  = H1D(\"Z leptonic recoil (zoom)\", 200, 120, 140, unit=:GeV),\n",
    "    lr_m_zoom3  = H1D(\"Z leptonic recoil (zoom)\", 400, 120, 140, unit=:GeV),\n",
    "    lr_m_zoom4  = H1D(\"Z leptonic recoil (zoom)\", 800, 120, 140, unit=:GeV),\n",
    "    lr_m_zoom5  = H1D(\"Z leptonic recoil (zoom)\", 100, 130.3, 132.5, unit=:GeV),\n",
    "    mz_lr_m     = H2D(\"m_{Z} vs Z leptonic recoil\", 40, 80, 100, 100, 120, 140, units=(:GeV, :GeV)),\n",
    ");"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Open the data file to get the events\n",
    "- It is using a file in EOS with the `root:` protocol\n",
    "- The obtained `events` is a `LazyTree` created by the [UnROOT.jl](https://github.com/JuliaHEP/UnROOT.jl) package.\n",
    "  As the name indicates, the event is actually yet read."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# f = \"root://eospublic.cern.ch//eos/experiment/fcc/ee/generation/DelphesEvents/winter2023/IDEA/p8_ee_ZZ_ecm240/events_000189367.root\"\n",
    "f = joinpath(@__DIR__, \"../../../examples/FCC/events_000189367.root\")\n",
    "\n",
    "reader = RootIO.Reader(f);\n",
    "events = RootIO.get(reader, \"events\");"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Loop over events and fill the histograms"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "@time for evt in events\n",
    "    muids = RootIO.get(reader, evt, \"Muon#0\")     # get the ObjectIDs of Muons\n",
    "    length(muids) < 2 && continue                 # need at least 2 muons to build a Z\n",
    "    recps = RootIO.get(reader, evt, \"ReconstructedParticles\")\n",
    "    muons = recps[muids]                          # use the objectIDs to collect the referenced ReconstructedParticles\n",
    "    sel_muons = filter(x -> pâ‚œ(x) > 10GeV, muons)  # select muons with pT > 10 GeV\n",
    "    zed_leptonic = resonanceBuilder(91GeV, sel_muons)\n",
    "    zed_leptonic_recoil = recoilBuilder(240GeV, zed_leptonic)\n",
    "    if length(zed_leptonic) == 1                  #  Filter to have exactly one Z candidate\n",
    "        Zcand_m        = zed_leptonic[1].mass\n",
    "        Zcand_recoil_m = zed_leptonic_recoil[1].mass\n",
    "        Zcand_q        = zed_leptonic[1].charge\n",
    "        if 80GeV <= Zcand_m <= 100GeV\n",
    "            ##---Fill histograms now--------------------------------------\n",
    "            push!(myhists.mz, Zcand_m)\n",
    "            push!(myhists.mz_zoom, Zcand_m)\n",
    "            push!(myhists.lr_m, Zcand_recoil_m)\n",
    "            push!(myhists.lr_m_zoom, Zcand_recoil_m)\n",
    "            push!(myhists.lr_m_zoom1, Zcand_recoil_m)\n",
    "            push!(myhists.lr_m_zoom2, Zcand_recoil_m)\n",
    "            push!(myhists.lr_m_zoom3, Zcand_recoil_m)\n",
    "            push!(myhists.lr_m_zoom4, Zcand_recoil_m)\n",
    "            push!(myhists.lr_m_zoom5, Zcand_recoil_m)\n",
    "            push!(myhists.mz_lr_m, Zcand_m, Zcand_recoil_m)\n",
    "        end\n",
    "    end\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "## Plot the results"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "plot((plot(h) for h in myhists)..., layout=(5,2), size=(1200,1500))"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.1"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.1",
   "language": "julia"
  }
 },
 "nbformat": 4
}